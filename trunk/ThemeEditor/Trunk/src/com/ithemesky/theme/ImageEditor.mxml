<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="468" height="680" xmlns:theme="com.ithemesky.theme.*" creationComplete="canvas1_creationCompleteHandler(event)">
	<mx:Script>
		<![CDATA[
			import com.ithemesky.event.EditorEvent;
			import com.just.editor.PictureCanvas;
			
			import mx.events.FlexEvent;

			private var _picCanvas:PictureCanvas;
			private var _file:FileReference;
			private var _loader:Loader;
			protected function canvas1_creationCompleteHandler(event:FlexEvent):void
			{
				//初始化图片界面
				_picCanvas = new PictureCanvas();
				_picCanvas.x=10;
				_picCanvas.y=10;
				editor.rawChildren.addChild(_picCanvas);
				_picCanvas.Relayout(editor.width-10, editor.height-10);
				//_picCanvas.LoadPictureByUrl("Resources/Image/Wallpaper.png");
				this.parent.dispatchEvent(new EditorEvent(EditorEvent.LOAD_COMPLETE));
				
				//文件选择添加事件
				_file=new FileReference();
				_file.addEventListener(Event.COMPLETE, fileHandler);
				_file.addEventListener(Event.SELECT, fileHandler);
				_loader=new Loader();
				_loader.contentLoaderInfo.addEventListener(Event.COMPLETE, loaderComplete);
				
				//Init editor button's event
				for (var i:Number=0; i<this.numChildren; i++){
					var child:DisplayObject = this.getChildAt(i);
					if (child is EditorButton) {
						child.addEventListener(MouseEvent.CLICK, editorbutton_clickHandler);
					}
				}
			}
			
			public function LoadImage(url:String):void
			{
				_picCanvas.LoadPictureByUrl(url);
			}


			protected function editorbutton_clickHandler(event:MouseEvent):void
			{
				var button:EditorButton = EditorButton(event.currentTarget);
				switch (button.ImageName) {
					case "editor_open":
						_file.browse(new Array(new FileFilter("Images (*.jpg, *.jpeg, *.gif, *.png)", "*.jpg;*.jpeg;*.gif;*.png")));
						break;
					case "editor_cut":
						_picCanvas.ShowSelector(true);
						break;
					case "editor_move":
						_picCanvas.ShowSelector(false);
						break;
					case "editor_realistic":
						_picCanvas.SetRealSize();
						break;
					case "editor_suit":
						_picCanvas.SetAproposSize();
						break;
					case "editor_rotate_left":
						_picCanvas.LeftRotate();
						break;
					case "editor_rotate_right":
						_picCanvas.RightRotate();
						break;
					case "editor_rotate_v":
						_picCanvas.HorizontalRotate();
						break;
					case "editor_rotate_h":
						_picCanvas.VerticalRotate();
						break;
					case "editor_img_original":
						_picCanvas.RestorePicture();
						break;
					case "btn_back":
						this.parent.dispatchEvent(new EditorEvent(EditorEvent.BACK));
						break;
				}
			}
			
			/**
			 * 图片读取完毕，载入图片
			 */
			private function loaderComplete(e:Event):void
			{
				var loader:Loader=Loader(e.target.loader);
				var bmpData:BitmapData=Bitmap(loader.content).bitmapData;
				this._picCanvas.LoadPicture(bmpData);
			}
			
			/**
			 * 打开本地图片
			 */
			private function fileHandler(e:Event):void
			{
				var fileOpen:FileReference=e.target as FileReference;
				switch (e.type)
				{
					case Event.SELECT:
						fileOpen.load(); //仅在flash player10中支持
						break;
					case Event.COMPLETE:
						_loader.loadBytes(fileOpen.data); //仅在flash player10中支持
						break;
				}
			}

		]]>
	</mx:Script>
	<mx:Canvas id="editor" x="40" y="45" width="408" height="568" backgroundImage="Resources/Image/editor_bg.png">
	</mx:Canvas>
	<theme:EditorButton x="45" y="16" ImageName="editor_open"/>
	<theme:EditorButton x="79" y="16" ImageName="editor_cut"/>
	<theme:EditorButton x="113" y="16" ImageName="editor_move"/>
	<theme:EditorButton x="149" y="16" ImageName="editor_zoom"/>
	<theme:EditorButton x="181" y="16" ImageName="editor_realistic"/>
	<theme:EditorButton x="216" y="16" ImageName="editor_suit"/>
	<theme:EditorButton x="251" y="16" ImageName="editor_rotate_left"/>
	<theme:EditorButton x="284" y="16" ImageName="editor_rotate_right"/>
	<theme:EditorButton x="318" y="16" ImageName="editor_rotate_v"/>
	<theme:EditorButton x="353" y="16" ImageName="editor_rotate_h"/>
	<theme:EditorButton x="387" y="16" ImageName="editor_img_original"/>	
	<theme:EditorButton x="421" y="16" ImageName="editor_icon_original"/>
	<theme:EditorButton x="46" y="637" ImageName="btn_back"/>
	<theme:EditorButton x="113" y="637" ImageName="btn_add_icon"/>
	<theme:EditorButton x="250" y="625" ImageName="guide_btn_save"/>
</mx:Canvas>
